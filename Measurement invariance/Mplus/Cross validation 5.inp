Title: Testing for Equivalence of Causal Structure
Across Calibration & Validation Groups
Factor Loadings, Intercepts, Structural Paths Constrained Equal;

Data: 
FILE (Calib) IS elemind1.dat;
FILE (Valid) IS elemind2.dat;
FORMAT IS (19F4.2/13F4.2);

Variable:
NAMES ARE rolea1 rolea2 rolec1 rolec2 work1 work2 cclim1 cclim2 cclim3
cclim4 dec1 dec2 ssup1 ssup2 psup1 psup2 self1 self2 self3 elc1
elc2 elc3 elc4 elc5 ee1 ee2 ee3 dp1 dp2 pa1 pa2 pa3;
USEVARIABLES ARE rolec1-ssup2 self1-pa3; !deleted variables

Analysis:
TYPE = GENERAL;
ESTIMATOR = MLM;

MODEL:
!all factor loadings constrained equal
F1 by rolec1-rolec2 work1-work2;
F2 by cclim1-cclim4;
F3 by dec1-dec2;
F4 by ssup1-ssup2;
F5 by self1-self3;
F6 by elc1-elc5;
F7 by ee1-ee3;
F8 by dp1-dp2;
F9 by pa1-pa3;

!all structural paths constrained equal
F5 on F3(1);
F5 on F4(2);
F6 on F1(3);
F7 on F1(4);
F7 on F2(5);
F8 on F7(6);
F9 on F5(7);
F9 on F7(8);
F9 on F8(9);

F9 with F6@0;
 
EE1 with EE2;

!factor means fixed to zero
[F1-F9@0]

Output: MODINDICES TECH1;
