TITLE: HK Cancer Study
    Latent Growth Curve
    Model with Covariates;
!now we are trying to explain the heterogeneity
!i.e., the interindividual differences

DATA: file=hkcancer_red2.dat;
 
variable:
NAMES = AGE_GP SURG_TX mood2 mood3 mood4
socadj2 socadj3 socadj4 age age2 surg_tx2;
!these new variables are our predictor variables

USEVARIABLES = mood2 mood3 mood4 socadj2
socadj3 socadj4 age2 surg_tx2;

MISSING IS *;
!telling Mplus how our missing data is identified;

Analysis:
estimator IS MLR;
!using MLR estimator - std. errors are robust - 
!bc of missing data;

Model:

!this is the same as before
I1 by mood2@1 mood3@1 mood4@1;
!intercept for mood (I1) is measured by mood2 mood3 mood4;
S1 by mood2@0 mood3@1 mood4@2.33;
!slope for mood (I1) is measured by mood2 mood3 mood4;
I2 by socadj2@1 socadj3@1 socadj4@1;
!intercept for social adjustment (I2) is measured by
!socadj2 socadj3 socadj4
S2 by socadj2@0 socadj3@1 socadj4@2.33;
!slope for social adjustment (I2) is measured by
!socadj2 socadj3 socadj4

I1 with S1@0;
I2 with S1@0;
S2 with I1@0;
S2 with S1@0;
I2 with S2;
!non-significant covariances from previous model fixed to 0

socadj3 with mood3;

[mood2-mood4@0];
[socadj2-socadj4@0];
!groups variables as outcomes in model
!@0 specification relates to intercepts of variables, fixed
!to zero by default

[I1 S1]
!intercept & slope for mood serve as a set of parameters in model
[I2 S2];
!ditto for intercept & slope for social adjustment

I1 S1 on age2 surg_tx2;
I2 S2 on age2 surg_tx2;
!specified predictor regression paths
!including these new predictors to try to explain
!the interindividual differences


Output: stdyx;