Title: Validating Hypothesized Causal Structure for Calibration Group;
!Establishing the baseline model for calibration group only;
!test hypothesized model and then modify for best fit

Data: 
FILE IS elemind1.dat;
FORMAT IS (19F4.2/13F4.2);

Variable:
NAMES ARE rolea1 rolea2 rolec1 rolec2 work1 work2 cclim1 cclim2 cclim3
cclim4 dec1 dec2 ssup1 ssup2 psup1 psup2 self1 self2 self3 elc1
elc2 elc3 elc4 elc5 ee1 ee2 ee3 dp1 dp2 pa1 pa2 pa3;
USEVARIABLES ARE rolea1-pa3;

Analysis:
TYPE = GENERAL;
ESTIMATOR = MLM;

MODEL:
F1 by rolea1-rolea2;
F2 by rolec1-rolec2;
F3 by work1-work2;
F4 by cclim1-cclim4;
F5 by dec1-dec2;
F6 by ssup1-ssup2;
F7 by psup1-psup2;
F8 by self1-self3;
F9 by elc1-elc5;
F10 by ee1-ee3;
F11 by dp1-dp2;
F12 by pa1-pa3;

F8 on F5 F6 F7;
F9 on F5;
F10 on F2 F3 F4;
F11 on F2 F10;
F12 on F1 F8 F9 F10 F11;

Output: MODINDICES STDYX;
