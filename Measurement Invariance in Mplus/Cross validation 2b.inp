Title: Validating Hypothesized Causal Structure for Calibration Group
Combining Factors 2 and 3 into one factor;
Adding F8 on F2;
Adding EE1 with EE2;

Data: 
FILE IS elemind1.dat;
FORMAT IS (19F4.2/13F4.2);

Variable:
NAMES ARE rolea1 rolea2 rolec1 rolec2 work1 work2 cclim1 cclim2 cclim3
cclim4 dec1 dec2 ssup1 ssup2 psup1 psup2 self1 self2 self3 elc1
elc2 elc3 elc4 elc5 ee1 ee2 ee3 dp1 dp2 pa1 pa2 pa3;
USEVARIABLES ARE rolea1-pa3;

Analysis:
TYPE = GENERAL;
ESTIMATOR = MLM;

MODEL:
F1 by rolea1-rolea2;
F2 by rolec1-rolec2 work1-work2;
!based on previous model, we combined factor 2 & 3 
!(and then renumbered other factors)
F3 by cclim1-cclim4;
F4 by dec1-dec2;
F5 by ssup1-ssup2;
F6 by psup1-psup2;
F7 by self1-self3;
F8 by elc1-elc5;
F9 by ee1-ee3;
F10 by dp1-dp2;
F11 by pa1-pa3;

F7 on F4 F5 F6;
F8 on F4 F2;
F9 on F2 F3;
F10 on F2 F9;
F11 on F1 F7 F8 F9 F10;

 
EE1 with EE2;

Output: MODINDICES STDYX;
